plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "signing"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = '2.0.0' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
sourceCompatibility = 17

ext{
    buildNumber = System.getProperty("build.number")
    docx4JVersion = System.getProperty("docx4j.version") ? System.getProperty("docx4j.version") : '11.4.9'
}

dependencies {
    implementation("commons-io:commons-io:2.11.0")
    implementation("org.javassist:javassist:3.29.2-GA")
    implementation("org.springframework:spring-expression:6.0.11")
    implementation("jakarta.xml.bind:jakarta.xml.bind-api:4.0.0")

    // We want docx-stamper to be usable with any docx4j version, so the dependency to docx4J
    // has to be provided by the client.
    compileOnly("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")
    
    testImplementation("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")
    testImplementation("junit:junit:4.4")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "thombergs"
            name "Tom Hombergs"
            email "tom.hombergs@gmail.com"
        }
    }

    scm {
        url "https://github.com/thombergs/docx-stamper"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId = 'io.reflectoring'
            artifactId = 'docx-stamper'
            version = project.version

            pom {
                name = 'DocxStampler'
                description = 'Template engine for .docx documents.'
                url = 'https://github.com/thombergs/docx-stamper'
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = 'thombergs'
                        name = 'Tom Hombergs'
                        email = 'tom@reflectoring.io'
                    }
                }
                scm {
                    connection = 'https://github.com/thombergs/docx-stamper'
                    developerConnection = 'https://github.com/thombergs/docx-stamper'
                    url = 'https://github.com/thombergs/docx-stamper'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = new String(Base64.getDecoder().decode(System.getenv("SIGNING_KEY_BASE64")))
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign configurations.archives
    sign publishing.publications.mavenPublication
}

// we don't want to sign the artifacts unless we publish
signArchives.onlyIf{gradle.startParameter.getTaskNames().contains("publish")}
